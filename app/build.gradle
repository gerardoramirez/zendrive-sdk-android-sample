apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.zendrive.zendrivesdkdemo"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        resValue "bool", "isTestingMode", "false"
        buildConfigField "Class<? extends LayoutHandler>",
                "layoutHandler", "DefaultLayoutHandler.class"
    }

    buildTypes {
        zendrivesdktesting {
            initWith debug
            resValue "bool", "isTestingMode", "true"
            buildConfigField "Class<? extends LayoutHandler>",
                    "layoutHandler",
                    "MockDriveLayoutHandler.class"
            applicationIdSuffix '.zendrivesdktesting'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        // This is needed to avoid spurious lint errors from libthrift on android.
        disable 'InvalidPackage'
        abortOnError true
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    dataBinding {
        enabled true
    }
}

dependencies {
    def supportLibVersion = '28.0.0'
    def zendrive_sdk_version = '5.10.0'
    implementation "com.zendrive.sdk.android:ZendriveSDK:$zendrive_sdk_version"
    implementation 'com.google.android.gms:play-services-location:11.8.0'
    implementation 'com.google.android.gms:play-services-maps:11.8.0'
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support:support-vector-drawable:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'

    zendrivesdktestingImplementation "com.zendrive.sdk.android:ZendriveSDK-testing:$zendrive_sdk_version"
}
